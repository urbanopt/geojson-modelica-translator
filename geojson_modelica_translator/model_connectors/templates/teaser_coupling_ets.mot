within {{project_name}}.Loads.{{model_name}};
model teaser_coupling_ets
    "TEASER building connected to indirect cooling ETS"
  extends Modelica.Icons.Example;
  package MediumW = Buildings.Media.Water
    "Source side medium";
  package MediumA = Buildings.Media.Air
    "Load side medium";
{% raw %}
  parameter Modelica.SIunits.MassFlowRate m1_flow_nominal=
   (bui.disFloCoo.m_flow_nominal.*(bui.delTBuiCoo/delTDisCoo))
    "Nominal mass flow rate of primary (district) district cooling side";
  parameter Modelica.SIunits.MassFlowRate m2_flow_nominal= bui.disFloCoo.m_flow_nominal
    "Nominal mass flow rate of secondary (building) district cooling side";
  parameter Modelica.SIunits.TemperatureDifference delTDisCoo=10
    "Nominal district supply and return water temperature difference";

  CoolingIndirect coo(
   redeclare package Medium = MediumW,
    m1_flow_nominal=m1_flow_nominal,
    m2_flow_nominal=m2_flow_nominal,
    dp1_nominal=500,
    dp2_nominal=500,
    dpValve_nominal=7000,
    use_Q_flow_nominal=true,
    Q_flow_nominal=-1*(sum(bui.terUni.QCoo_flow_nominal)),
    T_a1_nominal=273.15 + 8,
    T_a2_nominal=273.15 + 19)
    "Indirect cooling energy transfer station ETS."
      annotation (Placement(transformation(extent={{-4,-60},{20,-40}})));
  building bui(
    have_pum=false,
    have_heaLoa=true,
    have_cooLoa=true,
    have_weaBus=true,
    nPorts_bCoo=1,
    nPorts_bHea=1,
    nPorts_aHea=1,
    nPorts_aCoo=1)
    "Building model integrating multiple time series thermal zones."
    annotation (Placement(transformation(extent={{0,60},{20,80}})));

  Buildings.Fluid.Sources.MassFlowSource_T supChiWat(
    redeclare package Medium = MediumW,
    use_m_flow_in=true,
    use_T_in=false,
    T=281.15,
    nPorts=1) "Chilled water supply (district side)."
     annotation (Placement(transformation(
        extent={{-10,-10},{10,10}},
        rotation=0,
        origin={-30,-30})));

  Buildings.Fluid.Sources.Boundary_pT sinChiWat(
    redeclare package Medium = MediumW, nPorts=1)
   "Sink for chilled water"
     annotation (Placement(transformation(
        extent={{10,-10},{-10,10}},
        rotation=0,
        origin={70,-30})));
  Modelica.Blocks.Sources.RealExpression TChiWatSet(y=14 + 273.15)
    "Primary loop (district side) chilled water setpoint temperature."
     annotation (Placement(transformation(extent={{-80,-20},{-60,0}})));
  Modelica.Blocks.Sources.RealExpression THeaWatSup(
    y=max(bui.terUni.T_aHeaWat_nominal))
    "Heating water supply temperature"
     annotation (Placement(transformation(extent={{-80,20},{-60,40}})));

  Buildings.Fluid.Sources.MassFlowSource_T supHeaWat(
      redeclare package Medium = MediumW,
      use_m_flow_in=true,
      use_T_in=true,
      nPorts=1) "Heating water supply" annotation (Placement(transformation(
          extent={{-10,-10},{10,10}},
          rotation=0,
          origin={-30,40})));
  Buildings.Fluid.Sources.Boundary_pT sinHeaWat(redeclare package Medium =
          MediumW, nPorts=1)
       "Sink for heating water" annotation (Placement(transformation(
            extent={{10,-10},{-10,10}},
            rotation=0,
            origin={50,40})));
  Modelica.Blocks.Sources.RealExpression mHeaWatSup(y=bui.disFloHea.mReqTot_flow)
      "Heating water supply mass flow rate."
       annotation (Placement(transformation(extent={{-80,28},{-60,48}})));

  Modelica.Blocks.Sources.RealExpression secMasFloRat(
    y=bui.disFloCoo.mReqTot_flow) "Secondary loop chilled water flow rate."
     annotation (Placement(transformation(extent={{-80,-40},{-60,-20}})));
  Modelica.Blocks.Sources.RealExpression priMasFloRat(
    y=bui.disFloCoo.mReqTot_flow.* (bui.delTBuiCoo/delTDisCoo))
    "Primary loop chilled water flow rate."
     annotation (Placement(transformation(extent={{-80,0},{-60,20}})));
  Buildings.Fluid.Movers.FlowControlled_m_flow pumBui(
    redeclare package Medium = MediumW,
    energyDynamics=Modelica.Fluid.Types.Dynamics.FixedInitial,
    m_flow_nominal=sum(bui.terUni.mChiWat_flow_nominal .* bui.terUni.facSca),
    inputType=Buildings.Fluid.Types.InputType.Continuous,
    nominalValuesDefineDefaultPressureCurve=true,
    use_inputFilter=false,
    dp_nominal=6000)
     "Building-side (secondary) pump."
     annotation (Placement(transformation(extent={{-40,-82},{-60,-62}})));
  Modelica.Fluid.Sources.FixedBoundary pre(
    redeclare package Medium = MediumW,
    p=300000,
    use_T=false,
    nPorts=1)
    "Pressure source"
     annotation (Placement(transformation(
        extent={{10,-10},{-10,10}},
        rotation=0,
        origin={70,-72})));
  Buildings.BoundaryConditions.WeatherData.ReaderTMY3 weaDat(
    calTSky=Buildings.BoundaryConditions.Types.SkyTemperatureCalculation.HorizontalRadiation,
    pAtm(displayUnit="Pa") = 101339,
    filNam=ModelicaServices.ExternalReferences.loadResource("modelica://Buildings/Resources/weatherdata/USA_IL_Chicago-OHare.Intl.AP.725300_TMY3.mos"),
    computeWetBulbTemperature=true)
    "Weather data reader"
    annotation (Placement(transformation(extent={{10,-10},{-10,10}},
        rotation=90,
        origin={50,90})));

{% endraw %}
equation
{% raw %}
      connect(weaDat.weaBus, bui.weaBus) annotation (Line(
        points={{40,30},{30,30},{30,-22.8667},{50.0333,-22.8667}},
        color={255,204,51},
        thickness=0.5));
      connect(coo.TSet, TChiWatSet.y)
        annotation (Line(points={{-2,-50},{-48,-50},{-48,
               -10},{-59,-10}}, color={0,0,127}));
       connect(supHeaWat.T_in, THeaWatSup.y)
        annotation (Line(points={{-42,34},{-46,34},{-46,30},{-59,30}}, color={0,0,127}));
       connect(secMasFloRat.y, pumBui.m_flow_in)
        annotation (Line(points={{-59,-30},{-50,-30},{-50,-60}}, color={0,0,127}));
       connect(coo.port_a1, supChiWat.ports[1])
        annotation (Line(points={{0,-44},{-14,-44},{-14,-30},{-20,-30}}, color={0,127,255}));
       connect(coo.port_b2, pumBui.port_a)
        annotation (Line(points={{0,-56},{0,-72},{-40,-72}},color={0,127,255}));
       connect(coo.port_b2, pre.ports[1])
        annotation (Line(points={{0,-56},{0,-72},{60,-72}},color={0,127,255}));
       connect(supChiWat.m_flow_in, priMasFloRat.y)
        annotation (Line(points={{-42,-22},{-46,-22},{-46,10},{-59,10}}, color={0,0,127}));
       connect(sinChiWat.ports[1], coo.port_b1)
        annotation (Line(points={{60,-30},{40,-30},{40,-44},{20,-44}}, color={0,127,255}));
       connect(supHeaWat.ports[1], bui.secHeaSup[1])
        annotation (Line(points={{-20,30},{-8,30},{-8,64},{0,64}}, color={0,127,255}));
       connect(sinHeaWat.ports[1], bui.secHeaRet[1])
        annotation (Line(points={{60,30},{28,30},{28,64},{20,64}}, color={0,127,255}));
       connect(pumBui.port_b, bui.secCooSup[1])
        annotation (Line(points={{-60,-72},{-88,-72},{-88,67.0667},{0,67.0667}}, color={0,127,255}));
       connect(coo.port_a2, bui.secCooRet[1])
        annotation (Line(points={{20,-56},{92,-56},{92,67},{20,67}}, color={0,127,255}));
       connect(mHeaWatSup.y, supHeaWat.m_flow_in)
        annotation (Line(points={{-59,48},{-42,48}}, color={0,0,127}));
  annotation (
{% endraw %}
   __Dymola_Commands(file="modelica://{{project_name}}/Loads/Resources/Scripts/{{model_name}}/Dymola/RunCouplingETS_TEASERBuilding.mos"
          "Simulate and plot"),
          experiment(StopTime=86400,
                     Tolerance=1e-06),
  Documentation(info="<html>
<p>
This example illustrates the use of
<a href=\"modelica://Buildings.Applications.DHC.Loads.BaseClasses.PartialBuilding\">
Buildings.Applications.DHC.Loads.BaseClasses.PartialBuilding</a>,
<a href=\"modelica://Buildings.Applications.DHC.Loads.BaseClasses.PartialTerminalUnit\">
Buildings.Applications.DHC.Loads.BaseClasses.PartialTerminalUnit</a>
and
<a href=\"modelica://Buildings.Applications.DHC.Loads.BaseClasses.FlowDistribution\">
Buildings.Applications.DHC.Loads.BaseClasses.FlowDistribution</a>
in a configuration with
</p>
<ul>
<li>
an n-zone building model based on a two-element reduced order model (from
GeoJSON export), and
</li>
<li>
secondary pumps.
</li>
</ul>
</html>",
revisions=
"<html>
<ul>
<li>
May 29, 2020, by Nicholas Long:<br/>
ETS Templating for TEASER.
</li>
<li>
February 21, 2020, by Antoine Gautier:<br/>
First implementation.
</li>
</ul>
</html>"));
end teaser_coupling_ets;
